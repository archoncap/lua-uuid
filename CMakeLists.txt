# $Id$

PROJECT (lua-uuid)
CMAKE_MINIMUM_REQUIRED (VERSION 2.8)

FIND_PACKAGE (Lua51 REQUIRED)
INCLUDE_DIRECTORIES (${LUA_INCLUDE_DIR})

SET (lua_uuid_SOURCE_FILES luuid.c)

IF (WIN32)
	SET (lua_uuid_SOURCE_FILES ${lua_uuid_SOURCE_FILES} wuuid.c)
ELSE(WIN32)
	FIND_LIBRARY (UUID_LIBRARY NAMES uuid HINTS PATH_SUFFIXES lib64 lib PATHS /usr)
	IF (NOT UUID_LIBRARY)
		MESSAGE (FATAL_ERROR "Not found libuuid.")
	ENDIF (NOT UUID_LIBRARY)
ENDIF (WIN32)

SET (lua_uuid_SHARED uuid)
ADD_LIBRARY (${lua_uuid_SHARED} SHARED ${lua_uuid_SOURCE_FILES})
SET_TARGET_PROPERTIES (${lua_uuid_SHARED} PROPERTIES PREFIX "")
TARGET_LINK_LIBRARIES (${lua_uuid_SHARED} ${UUID_LIBRARY})
INSTALL (TARGETS ${lua_uuid_SHARED} DESTINATION lib/lua/5.1)

SET (CPACK_GENERATOR "DEB")
SET (CPACK_PACKAGE_NAME "liblua5.1-uuid")

SET (CPACK_PACKAGE_VERSION_MAJOR "1")
SET (CPACK_PACKAGE_VERSION_MINOR "0")
SET (CPACK_PACKAGE_VERSION_PATCH "0")
SET (CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET (CPACK_PACKAGE_CONTACT "Like Ma <likemartinma@gmail.com>")

FIND_PROGRAM(DPKG_CMD dpkg)
IF (DPKG_CMD)
	EXECUTE_PROCESS (COMMAND "${DPKG_CMD}" --print-architecture
		OUTPUT_VARIABLE CPACK_DEBIAN_PACKAGE_ARCHITECTURE
		OUTPUT_STRIP_TRAILING_WHITESPACE)
ELSE (DPKG_CMD)
	MESSAGE (STATUS "CPackDeb: Can not find dpkg in your path, default to i386.")
	SET (CPACK_DEBIAN_PACKAGE_ARCHITECTURE i386)
ENDIF (DPKG_CMD)

SET (CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}_${CPACK_PACKAGE_VERSION}_${CPACK_DEBIAN_PACKAGE_ARCHITECTURE}")
SET (CPACK_SOURCE_PACKAGE_FILE_NAME ${CPACK_PACKAGE_FILE_NAME})

SET (CPACK_PACKAGE_DESCRIPTION_SUMMARY "UUID library for the Lua language version 5.1
  This package contains uuid library for lua.")

SET (CPACK_DEBIAN_PACKAGE_SECTION "libs")
SET (CPACK_DEBIAN_PACKAGE_HOMEPAGE "http://www.tecgraf.puc-rio.br/~lhf/ftp/lua")
SET (CPACK_DEBIAN_PACKAGE_SHLIBDEPS ON)

INCLUDE (CPack)
